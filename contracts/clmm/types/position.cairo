use core::num::traits::{OverflowingAdd, WideMul, Zero};
use crate::types::fees_per_liquidity::FeesPerLiquidity;

// Represents a liquidity position
// Packed together in a single struct because whenever liquidity changes we typically change fees
// per liquidity as well
#[derive(Copy, Drop, Serde, starknet::Store)]
pub struct Position {
    // the amount of liquidity owned by the position
    pub liquidity: u128,
    // the fee per liquidity inside the tick range of the position, the last time it was computed
    pub fees_per_liquidity_inside_last: FeesPerLiquidity,
}

// we only check liquidity is non-zero because fees per liquidity inside is irrelevant if liquidity
// is 0
impl PositionZero of Zero<Position> {
    fn zero() -> Position {
        Position { liquidity: Zero::zero(), fees_per_liquidity_inside_last: Zero::zero() }
    }

    fn is_zero(self: @Position) -> bool {
        self.liquidity.is_zero()
    }

    fn is_non_zero(self: @Position) -> bool {
        !self.liquidity.is_zero()
    }
}

pub(crate) fn multiply_and_get_limb1(a: u256, b: u128) -> u128 {
    // Return the low 128 bits of (a * b) >> 128 without panicking on overflow.
    let low_prod = WideMul::<u128, u128>::wide_mul(a.low, b);
    let high_prod = WideMul::<u128, u128>::wide_mul(a.high, b);
    let (sum, _) = OverflowingAdd::overflowing_add(low_prod.high, high_prod.low);
    sum
}

#[generate_trait]
pub impl PositionTraitImpl of PositionTrait {
    fn fees(self: Position, fees_per_liquidity_inside_current: FeesPerLiquidity) -> (u128, u128) {
        let diff = fees_per_liquidity_inside_current - self.fees_per_liquidity_inside_last;

        // we only use the lower 128 bits from this calculation, and if accumulated fees overflow a
        // u128 they are simply discarded we discard the fees instead of asserting because we do not
        // want to fail a withdrawal due to too many fees being accumulated this is an optimized
        // wide multiplication that only cares about limb1
        (
            multiply_and_get_limb1(diff.value0, self.liquidity),
            multiply_and_get_limb1(diff.value1, self.liquidity),
        )
    }
}
